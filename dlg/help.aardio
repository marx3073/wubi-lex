import win.ui;
import fonts.fontAwesome;
/*DSG{{*/
var winform = win.form(text="wubiLex - 帮助";right=1040;bottom=607;bgcolor=16777215)
winform.add(
keyboardShortcuts={cls="plus";left=76;top=51;right=934;bottom=438;align="left";border={color=-4144960;radius=8;width=1};db=1;dl=1;dr=1;dt=1;font=LOGFONT(h=-15);textPadding={left=10;top=10;right=10;bottom=10};valign="top";z=1};
lbAbout={cls="plus";text="关于 wubiLex ( Win10 五笔助手 ) :";left=46;top=448;right=388;bottom=479;align="left";db=1;dl=1;font=LOGFONT(h=-16);iconColor=2960685;iconStyle={align="left";font=LOGFONT(h=-19;name='FontAwesome')};iconText='\uF059';textPadding={left=30};z=3};
lbGitHub={cls="plus";text="开源代码:";left=325;top=484;right=415;bottom=515;align="right";db=1;dl=1;font=LOGFONT(h=-16);iconColor=2960685;z=7};
lbKeyboardShortcuts={cls="plus";text="微软五笔使用技巧( 建议大家更新Win10到较新的版本 ) :";left=46;top=13;right=594;bottom=44;align="left";dl=1;dr=1;dt=1;font=LOGFONT(h=-16);iconColor=2960685;iconStyle={align="left";font=LOGFONT(h=-19;name='FontAwesome')};iconText='\uF11C  ';textPadding={left=30};z=2};
lbWebSite={cls="plus";text="官网:";left=87;top=484;right=150;bottom=515;align="right";db=1;dl=1;font=LOGFONT(h=-16);iconColor=3947580;z=5};
lnkGitHub={cls="plus";text="https://github.com/aardio/wubi-lex";left=428;top=488;right=697;bottom=512;align="left";color=8388608;db=1;dl=1;dr=1;font=LOGFONT(h=-16);notify=1;textPadding={left=5};z=4};
lnkVersion={cls="plus";text="已经是最新版本";left=109;top=559;right=470;bottom=590;align="left";color=8388608;db=1;dl=1;font=LOGFONT(h=-16);iconColor=2960685;iconStyle={align="left";font=LOGFONT(h=-16;name='FontAwesome')};iconText='\uF021';notify=1;paddingLeft=20;z=8};
lnkWebSite={cls="plus";text="wubi.aardio.com";left=154;top=488;right=294;bottom=512;align="left";color=8388608;db=1;dl=1;font=LOGFONT(h=-16);iconColor=2960685;notify=1;textPadding={left=5};z=6};
plus={cls="plus";text="自动更新 :";left=46;top=524;right=388;bottom=555;align="left";db=1;dl=1;font=LOGFONT(h=-16);iconColor=2960685;iconStyle={align="left";font=LOGFONT(h=-19;name='FontAwesome')};iconText='\uF05A';textPadding={left=30};z=9}
)
/*}}*/

winform.keyboardShortcuts.text = /*
1、短按Shift键（或Ctrl + Space）：切换中英文输入。
五笔英文模式下，按Shift + 空格键，切换全角/半角输入。

2、中文模式下，可以使用小键盘输入数字和小数点（不是句号）。

3、按Alt + Shift键，切换中文/英文键盘。

4、Win键 + 空格键：切换输入法。

5、五笔编码自第二码开始可使用Z键通配任意编码（ 首码除外 ）

6、中文输入时，按tab鍵跳到表情符号，然后按空格键就可打开表情与符号软键盘。
注意：小键盘右上角 - 关闭按钮的左侧有一个『保持打开』的按钮。

7、输入时，按回车取消输入，按空格取首个候选词，
按分号键 ( ; ) 取第二个候选词，按引号键（'）取第三个候选词。

8、输入时，按 -，+ 键对候选词翻页。
*/

import style;
winform.lnkGitHub.skin(style.link)
winform.lnkWebSite.skin(style.link)
winform.lnkVersion.skin(style.link)

import process;
winform.lnkGitHub.oncommand = function(id,event){
	process.openUrl("https://github.com/aardio/wubi-lex")
}

winform.lnkWebSite.oncommand = function(id,event){
	process.openUrl("http://wubi.aardio.com/")
}

import fsys.update.dlMgr;
winform.lnkVersion.oncommand = function(id,event){
	if(_STUDIO_INVOKED){
		winform.msgboxErr("请先发布软件!")
		return;
	}
	
	var dlMgr = fsys.update.dlMgr("http://wubi.aardio.com/update/version.txt",..io.appData("aardio/wubiLex/update"));
	dlMgr.onConfirmDownload = function(isUpdated,appVersion,latestVersion,description){ 
		dlMgr[["latestVersion"]] = latestVersion;
		dlMgr[["updateDescription"]] = description;
		if(!isUpdated){
			winform.lnkVersion.disabledText = null;
			winform.lnkVersion.text = "已经是最新版本";
		}
		return isUpdated; 
	} 
	
	dlMgr.onEnd = function(updater,saveDir,appDir,mainPath){ 
		if(updater){
			globalUpdateInfo = {
				version = dlMgr[["latestVersion"]];
				description = dlMgr[["updateDescription"]];
			}; 
		}
		else {
			globalUpdateInfo = null;
		}
		
		winform.lnkVersion.disabledText = null;
		publish("wubiLexUpdated");
	};
	
	dlMgr.onError = function(err,filename){
		winform.lnkVersion.disabledText = null;
		winform.lnkVersion.text = err;
	}
	
	winform.lnkVersion.text = "正在检查新版本";
	winform.lnkVersion.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
	dlMgr.prepareUpdate();
}

import win.ui.tooltip;
var tooltipCtrl = win.ui.tooltip( winform );
tooltipCtrl.addTool(winform.lnkVersion,"点击检测更新" );

var updateVersion = function(){
	if(#globalUpdateInfo[["version"]]){
		winform.lnkVersion.text = "重新启动软件更新到新版本：" + globalUpdateInfo[["version"]]
			
		if(#globalUpdateInfo[["description"]]){
			tooltipCtrl.addTool(winform.lnkVersion,globalUpdateInfo[["description"]] )
		}
	}
	else {
		winform.lnkVersion.text = "已经是最新版本";
	} 
}

subscribe("wubiLexUpdated",updateVersion);
updateVersion();

winform.show();
win.loopMessage();
return winform;