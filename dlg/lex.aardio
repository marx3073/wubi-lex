//RUNAS//
import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="五笔码表管理﻿工具";right=1040;bottom=600;bgcolor=16777215)
winform.add(
bkplus={cls="bkplus";left=0;top=304;right=1041;bottom=600;bgcolor=16777215;db=1;dl=1;dr=1;forecolor=15780518;linearGradient=90;z=3};
btnAddFile={cls="plus";text='\uF271   添加码表文件';left=26;top=17;right=187;bottom=53;bgcolor=11580047;dl=1;dt=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;tabstop=1;z=1};
btnCopyToSystem={cls="plus";text='\uF0C7   安装到系统词库';left=196;top=17;right=389;bottom=53;bgcolor=11580047;disabled=1;dl=1;dt=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;tabstop=1;z=2};
lbTip={cls="plus";left=414;top=27;right=1009;bottom=61;color=2960685;font=LOGFONT(h=-16;name='FontAwesome');z=6};
listbox={cls="listbox";left=28;top=79;right=398;bottom=594;db=1;dl=1;dt=1;edge=1;font=LOGFONT(h=-16);items={};vscroll=1;z=4};
richedit={cls="richedit";left=404;top=79;right=1012;bottom=579;db=1;dl=1;dr=1;dt=1;edge=1;font=LOGFONT(h=-15);hscroll=1;multiline=1;vscroll=1;z=5}
)
/*}}*/

import fsys;
import fsys.dlg;
import fsys.wubiLex;
import win.inputBox;
import config;

var downloadDictFile = function(url,lexName){
	
	var lexPath = fsys.appdata("/aardio/std/wubi-lex-files/"+lexName+".lex");
	io.createDir(fsys.appdata("/aardio/std/wubi-lex-files/"))
	
    if( string.startWith(url,"http://",true) && (string.endWith(url,".lex",true))){
		var downBox = inet.downBox(winform,"下载词库:" + lexName,1000) 
		if( downBox.download(url,lexPath) ){
			var file = ..io.open(lexPath,"rb");
			if(!file){
				winform.msgboxErr(`错误的码表文件`)
				return;
			}
			
			if( file.read(#"imscwubi")!=="imscwubi"){
				winform.msgboxErr(`错误的码表文件`)
				file.close();
				return;
			}
		
			file.close(); 
			config.lex.files[lexName] = lexPath;
			config.saveAll();
			return lexPath;
		}
	}
}


var addDictFile = function(inputPath){
    if(!inputPath || ..fsys.isDir(inputPath)){ return; }
	
	var lexName;
	while(true){
		var inputBox = win.inputBox(winform)
		inputBox.text = "五笔码表管理﻿工具"
		inputBox.info.text = "请为码表指定一个友好名称"
		
		lexName = inputBox.doModal()
		if(!#lexName){
			return;
		}
			
		if(!fsys.path.validName(lexName)){
			winform.msgboxErr(`名称不能包含空格、回车、换行、以及 / : * ? " < > ] 等字符`)
			continue ;
		}
			
		if(config.lex.files[lexName]){
			winform.msgboxErr(`此名称已经存在，请重新指定`)
			continue ;
		}
	
		break ;
	}	
	
	var lexPath = fsys.appdata("/aardio/std/wubi-lex-files/"+lexName+".lex");
	io.createDir(fsys.appdata("/aardio/std/wubi-lex-files/"))
	
    if( string.startWith(inputPath,"http://",true) && (string.endWith(inputPath,".lex",true))){
		var downBox = inet.downBox(winform,"下载词库:" + lexName) 
		if( downBox.download(inputPath,lexPath) ){
			var file = ..io.open(inputPath,"rb");
			if(!file){
				winform.msgboxErr(`错误的码表文件`)
				return;
			}
			
			if( file.read(#"imscwubi")!=="imscwubi"){
				file.close();
				winform.msgboxErr(`错误的码表文件`)
				return;
			}
		
			file.close();
			
			config.lex.files[lexName] = lexPath;
			config.saveAll();
			return;
		}
	}
	
	if(string.endWith(inputPath,".txt",true)){
		var wubiLex = fsys.wubiLex(inputPath)
		if(!wubiLex){
			winform.msgboxErr(`错误的码表文件`)
			return;
		}
		wubiLex.saveLex(lexPath)		
	}
	else {
		var file = ..io.open(inputPath,"rb");
		if(!file){
			winform.msgboxErr(`错误的码表文件`)
			return;
		}
		
		if( file.read(#"imscwubi")!=="imscwubi"){
			winform.msgboxErr(`错误的码表文件`)
			return;
		}
		
		file.close()
		
		fsys.copy(inputPath,lexPath);
	}
	
	
	config.lex.files[lexName] = lexPath;
	table.push(config.lex.names,lexName);
	config.saveAll();
	
	winform.listbox.reloadLexList(); 	
}


winform.btnAddFile.oncommand = function(id,event){
	var inputPath = fsys.dlg.open("二进制码表(*.lex);文本码表(*.txt)|*.lex;*.txt||",,,winform)
	addDictFile(inputPath)
}

import process.admin;
process.admin.enableDropMsg(winform.hwnd);
winform.onDropFiles = function(files){
	addDictFile(files[1])
}
	
if(!#config.lex.names){
    
	config.lex.files["五笔98"] = "http://wubi.aardio.com/download/lex/ChsWubi98.lex";
	table.push(config.lex.names,"五笔98");
	
	config.lex.files["五笔86"] = "http://wubi.aardio.com/download/lex/ChsWubi86.lex";
	table.push(config.lex.names,"五笔86");
	
	config.saveAll();
}

winform.listbox.oncommand = function(id,event){ 
	if( event == 0x1/*_LBN_SELCHANGE*/ ){
		winform.btnCopyToSystem.disabled = winform.listbox.selIndex<=1 
		winform.lbTip.text = "";
	}
}

import inet.downBox
winform.listbox.wndproc = function(hwnd,message,wParam,lParam){
	select( message ) {   
		case 0x205/*_WM_RBUTTONUP*/{
			winform.lbTip.text = "";
			var x,y = win.getMessagePos();  
			var item = winform.listbox.hitTest(x,y,true); 
			if( item  ){
				winform.listbox.selIndex = item; 
				
				winform.popmenu = win.ui.popmenu(winform); 
				if(item>1){	
					winform.btnCopyToSystem.disabled = false;
					
					var lexName = config.lex.names[item-1];
					var lexPath = config.lex.files[lexName];
					
					if( string.startWith(lexPath,"http://",true) ){
						winform.popmenu.add('自服务器下载安装此词库',function(id){ 
							downloadDictFile(lexPath,lexName);
						} ) 	
						
						winform.popmenu.popup(x,y,true)
						return;
					}
					
					
					winform.popmenu.add('删除',function(id){ 
						var selIndex = winform.listbox.selIndex;
						winform.listbox.delete(selIndex)
						
						var lexName = config.lex.names[selIndex-1];
						if(lexName){
							var lexPath = config.lex.files[lexName];
							if(lexPath){
								io.remove(lexPath)
								config.lex.files[lexName] = null;
							}
							table.remove(config.lex.names,selIndex-1)
						}
					} ) 
					
					winform.popmenu.add('安装到系统词库',function(id){ 
						winform.btnCopyToSystem.oncommand();
					} )  				
				}
				else {
					winform.btnCopyToSystem.disabled = true;
				}
				
				
				winform.popmenu.add('导出文本格式词库',function(id){ 
					import process;
					var outputPath = fsys.dlg.save("文本码表（*.txt）|*.txt||",,,winform)
					if(!outputPath){
						return;
					}
					
					winform.lbTip.text = "正在导出数据"
		
						if(item>1){
						var lexName = config.lex.names[winform.listbox.selIndex-1];
						if(lexName){
							var lexPath = config.lex.files[lexName];
							var wubiLex = fsys.wubiLex(lexPath)
							
							wubiLex.saveTxt(outputPath) 
						} 
					}
					else {
						var wubiLex = fsys.wubiLex()
						wubiLex.saveTxt(outputPath) 
					}
					
					winform.lbTip.disabledText = null;
					winform.lbTip.text = "已完成"
				} ) 

				winform.popmenu.add('浏览字库文件',function(id){ 
					import process;
					if(item>1){
						var lexName = config.lex.names[winform.listbox.selIndex-1];
						if(lexName){
							var lexPath = config.lex.files[lexName]; 
							process.exploreSelect(lexPath)
						}
					}
					else {
						process.exploreSelect(fsys.wubiLex.path()) 
					}
				} ) 
				winform.popmenu.popup(x,y,true)
			}
		} 
	}
}

import tsfUtil;
winform.btnCopyToSystem.oncommand = function(id,event){
	var selIndex = winform.listbox.selIndex; 
	var lexName = config.lex.names[selIndex-1];
	if(lexName){  
		var srcPath = config.lex.files[lexName];
		if( string.startWith(srcPath,"http://",true) ){
			srcPath = downloadDictFile(srcPath,lexName); 
			if(!srcPath)return;
		}
		
		winform.richedit.setFocus(); 					
		winform.btnCopyToSystem.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}  
		var ok,err = win.invoke(copyToSystem,srcPath,winform) 
		publish("wubi.system.lex.changed");
		
		tsfUtil.activate();
		
		winform.lbTip.text = ok?'安装成功，如果不能输入，请按 Alt + Shift 来回切换一下即可': string.concat( "安装失败：",err);
		winform.btnCopyToSystem.disabledText = null; 
	}	
}

copyToSystem = function(srcPath,winform){
	import fsys;
	import service;
	import process;
	import tsfUtil;
	
	return tsfUtil.aclGrant(..fsys.getWinDir("\InputMethod\CHS\ChsWubiNew.lex"),
		function(chsWubiNewPath){
			return tsfUtil.aclGrant(..fsys.getWinDir("\InputMethod\CHS\ChsWubi.lex"),
				function(chsWubiPath){
					import tsfUtil;
					tsfUtil.reset(
						function(){ 
							for(i=1;10;1){
								process.kill("ctfmon.exe")
								process.kill("ChsIME.exe")
								
								if(chsWubiNewPath)fsys.delete(chsWubiNewPath )  
								if(chsWubiPath)fsys.delete(chsWubiPath ) 
								
								if((!io.exist(chsWubiNewPath)) && (!io.exist(chsWubiPath)) ){ break; }  
								sleep(1000)
							}
							
							var lexData = string.load(srcPath);
							if(chsWubiNewPath){ string.save(chsWubiNewPath,lexData); }
							if(chsWubiPath){ string.save(chsWubiPath,lexData); }
						}
						
					) 
					return true;
				}
			);		
		}
	);
}

import style
winform.btnAddFile.skin(style.primaryButton)
winform.btnCopyToSystem.skin(style.primaryButton)

winform.listbox.systemLexName = "系统码表";
winform.listbox.reloadLexList = function(){
	winform.listbox.clear();
	
	winform.listbox.add(winform.listbox.systemLexName)
	var lexNames = config.lex.names;
	for(i=1;#lexNames;1){
		winform.listbox.add(lexNames[i])
	} 
} 
winform.listbox.reloadLexList()

import win.debounce;
var updateSystemLexVersion = function(){
	winform.listbox.updateSystemLexVersion = function(ver){
		winform.listbox.systemLexName = "系统码表( " + ver + " )"
		winform.listbox.reloadLexList(); 
	}
	
	thread.invoke( 
		function(winform){
			import fsys.wubiLex;
			var wubiLex = fsys.wubiLex()
			var ver = wubiLex.getVersion();
			ver = ({["98"]="98版";["86"]="86版";["06"]="新世纪版"})[ver]; 
			winform.listbox.updateSystemLexVersion(ver)
		},winform
	)
}
updateSystemLexVersion();

subscribe("wubi.system.lex.changed",function(...){
	updateSystemLexVersion() 
} )

winform.richedit.setPadding(10,10,10,10)
winform.richedit.text =/*
可直接拖拽词库文件到当前窗口来添加词库。
可添加微软五笔二进制词库（*.lex），也可以添加文本格式的词库（*.txt）

文本格式词库可自动识别 UTF-8,UNICODE(UTF-16) 等各种编码，
词库每行一个编码，按键组合(小写字母)与输入文本间用空格或制表符分开。
下面的写法都是支持的，任何不符合格式要求的行都会自动忽略：
------------------------------
aaaa 工 㠭
戈 a 
节 ab
atn 薛
// 这是任性的注释行
*/

winform.show() 
win.loopMessage();