import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="设置";right=984;bottom=589;bgcolor=16777215)
winform.add(
btnSave={cls="plus";text='\uF045   更新设置';left=211;top=430;right=413;bottom=466;bgcolor=11580047;dl=1;dt=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;tabstop=1;z=4};
chkDefaultModeEn={cls="plus";text='\uF0C8 默认英文模式';left=215;top=305;right=357;bottom=336;align="left";dl=1;dt=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;z=2};
chkEnableWubiUdp={cls="plus";text='\uF0C8 允许自定义短语';left=214;top=348;right=356;bottom=379;align="left";dl=1;dt=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;z=7};
chkWubiAutoFinalize={cls="plus";text='\uF0C8 四码唯一时自动上屏（拼音混输时不建议开启）';left=216;top=266;right=656;bottom=297;align="left";dl=1;dt=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;z=9};
lbTip={cls="static";left=63;top=523;right=695;bottom=572;db=1;dl=1;dr=1;transparent=1;z=5};
plusOpenHotkeys={cls="plus";text='\uF11C   打开系统输入法热键设置';left=704;top=544;right=937;bottom=580;bgcolor=14935259;db=1;dr=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;tabstop=1;z=8};
plusOpenMsSetting={cls="plus";text='\uF17A   打开系统五笔设置页';left=485;top=544;right=687;bottom=580;bgcolor=14935259;db=1;dr=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;tabstop=1;z=6};
radioPinyinMixDisable={cls="plus";text='\uF111 纯五笔输入';left=215;top=192;right=377;bottom=223;align="left";dl=1;dt=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;z=1};
radioPinyinMixEnable={cls="plus";text='\uF111 五笔拼音混输';left=215;top=231;right=377;bottom=262;align="left";dl=1;dt=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;z=3}
)
/*}}*/

import style;
winform.radioPinyinMixEnable.skin(style.radio)
winform.radioPinyinMixDisable.skin(style.radio)
winform.chkDefaultModeEn.skin(style.checkBox)  
winform.chkEnableWubiUdp.skin(style.checkBox)
winform.chkWubiAutoFinalize.skin(style.checkBox)
winform.btnSave.skin(style.primaryButton)
winform.plusOpenMsSetting.skin(style.button)
winform.plusOpenHotkeys.skin(style.button)

import win.reg;
var regSettings = win.reg("HKEY_CURRENT_USER\Software\Microsoft\InputMethod\Settings\CHS")
var setting = regSettings.queryValueTable({
	["Default Mode"] = 1;
	["Enable Wubi EUDP"] = 1;
	["WubiAutoFinalizeForMixEnable"] = 1;
	["Wubi Auto-finalize Enable"] = 1;
})
winform.radioPinyinMixEnable.checked = setting["PinyinMixEnable"];
winform.radioPinyinMixDisable.checked = !setting["PinyinMixEnable"];
winform.chkDefaultModeEn.checked = setting["Default Mode"]
winform.chkEnableWubiUdp.checked = setting["Enable Wubi EUDP"]
if(winform.radioPinyinMixEnable.checked){
	winform.chkWubiAutoFinalize.checked = setting["WubiAutoFinalizeForMixEnable"]
}
else {
	winform.chkWubiAutoFinalize.checked = setting["Wubi Auto-finalize Enable"]
}

import tsfUtil;
winform.btnSave.oncommand = function(id,event){ 
	winform.btnSave.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
	
	regSettings.setDwValue("PinyinMixEnable",winform.radioPinyinMixEnable.checked?1:0)
	regSettings.setDwValue("Default Mode",winform.chkDefaultModeEn.checked?1:0)
	regSettings.setDwValue("Enable Wubi EUDP",winform.chkEnableWubiUdp.checked?1:0)
	if(winform.radioPinyinMixEnable.checked){
		regSettings.setDwValue("WubiAutoFinalizeForMixEnable",winform.chkWubiAutoFinalize.checked?1:0)
	}
	else {
		regSettings.setDwValue("Wubi Auto-finalize Enable",winform.chkWubiAutoFinalize.checked?1:0)
	}
	
	tsfUtil.reset()
	
	winform.lbTip.text = "更新成功，如果不能输入，请按 Alt + Shift 来回切换一下即可"
	winform.btnSave.disabledText = null;
}

import process;
winform.plusOpenMsSetting.oncommand = function(id,event){
	process.execute("ms-settings:regionlanguage-chsime-wubi");
}

import process.rundll;
winform.plusOpenHotkeys.oncommand = function(id,event){
	process.rundll(,"Control_RunDLL input.dll,,{C07337D3-DB2C-4D0B-9A93-B722A6C106E2}{HOTKEYS}")
}

winform.show();
win.loopMessage();
return winform;