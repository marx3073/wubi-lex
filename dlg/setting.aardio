import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="设置";right=984;bottom=589;bgcolor=16777215)
winform.add(
btnSave={cls="plus";text='\uF045   更新设置';left=601;top=467;right=803;bottom=503;db=1;dr=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;tabstop=1;z=4};
chkDefaultModeEn={cls="plus";text='\uF0C8 默认英文模式';left=269;top=282;right=411;bottom=313;align="left";font=LOGFONT(h=-16;name='FontAwesome');notify=1;z=2};
chkEnableWubiUdp={cls="plus";text='\uF0C8 允许自定义短语';left=268;top=325;right=410;bottom=356;align="left";font=LOGFONT(h=-16;name='FontAwesome');notify=1;z=7};
lbTip={cls="static";left=63;top=523;right=695;bottom=572;db=1;dl=1;dr=1;transparent=1;z=5};
plusOpenMsSetting={cls="plus";text='\uF17A   打开系统五笔设置页';left=354;top=467;right=556;bottom=503;db=1;dr=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;tabstop=1;z=6};
radioPinyinMixDisable={cls="plus";text='\uF111 纯五笔输入';left=269;top=192;right=431;bottom=223;align="left";dl=1;dt=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;z=1};
radioPinyinMixEnable={cls="plus";text='\uF111 五笔拼音混输';left=269;top=231;right=431;bottom=262;align="left";dl=1;dt=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;z=3}
)
/*}}*/

import style;
winform.radioPinyinMixEnable.skin(style.radio)
winform.radioPinyinMixDisable.skin(style.radio)
winform.chkDefaultModeEn.skin(style.checkBox)  
winform.chkEnableWubiUdp.skin(style.checkBox)
winform.btnSave.skin(style.button)
winform.plusOpenMsSetting.skin(style.button)

import win.reg;
var regSettings = win.reg("HKEY_CURRENT_USER\Software\Microsoft\InputMethod\Settings\CHS")
var setting = regSettings.queryValueTable({
	["Default Mode"] = 1;
	["Enable Wubi EUDP"] = 1;
})
winform.radioPinyinMixEnable.checked = setting["PinyinMixEnable"];
winform.chkDefaultModeEn.checked = setting["Default Mode"]
winform.chkEnableWubiUdp.checked = setting["Enable Wubi EUDP"]

import tsfUtil;
winform.btnSave.oncommand = function(id,event){ 
	winform.btnSave.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
	
	regSettings.setDwValue("PinyinMixEnable",winform.radioPinyinMixEnable.checked?1:0)
	regSettings.setDwValue("Default Mode",winform.chkDefaultModeEn.checked?1:0)
	regSettings.setDwValue("Enable Wubi EUDP",winform.chkEnableWubiUdp.checked?1:0)
	
	tsfUtil.reset()
	
	winform.lbTip.text = "更新成功，如果不能输入，请按 Alt + Shift 来回切换一下即可"
	winform.btnSave.disabledText = null;
}

import process;
winform.plusOpenMsSetting.oncommand = function(id,event){
	process.execute("ms-settings:regionlanguage-chsime-wubi");
}

winform.show();
win.loopMessage();
return winform;