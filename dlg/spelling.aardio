
import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="五笔码表管理﻿工具";right=1040;bottom=610;bgcolor=16777215)
winform.add(
editSearch={cls="plus";left=287;top=191;right=712;bottom=226;border={bottom=1;color=-16744448};dl=1;dr=1;dt=1;editable=1;font=LOGFONT(h=-16);tabstop=1;textPadding={top=6};z=3};
inkEdit={cls="atlax";text="InkEd.InkEdit";left=743;top=224;right=999;bottom=491;dr=1;dt=1;z=6};
lbInk={cls="static";text="手写输入:";left=743;top=201;right=863;bottom=218;dr=1;dt=1;transparent=1;z=7};
menuItem={cls="plus";text="测试菜单三";left=361;top=228;right=631;bottom=273;align="right";bgcolor=11525002;border={left=1;right=1;bottom=1;color=-3546113};tabstop=1;textPadding={right=20};z=2};
menuItem2={cls="plus";text="测试菜单三";left=361;top=273;right=631;bottom=318;align="right";bgcolor=11525002;border={left=1;right=1;bottom=1;color=-3546113};ont=LOGFONT(name='FontAwesome';charset=0);tabstop=1;textPadding={right=20};z=5};
plusCode={cls="plus";left=265;top=277;right=499;bottom=327;align="right";dl=1;dr=1;dt=1;font=LOGFONT(h=-37);textPadding={right=10};z=4};
plusDownloadSpellingGif={cls="plus";text='\uF019  下载五笔拆分详细图解字库';left=438;top=508;right=717;bottom=544;bgcolor=11580047;db=1;dr=1;font=LOGFONT(h=-16;name='FontAwesome');hide=1;notify=1;tabstop=1;z=8};
plusSpelling={cls="plus";left=311;top=348;right=688;bottom=404;color=255;dl=1;dr=1;dt=1;z=1};
plusSpellingGif={cls="plus";left=499;top=277;right=719;bottom=333;dl=1;dr=1;dt=1;repeat="scale";z=10};
plusTitle={cls="plus";text="根据当前安装的系统码表反查上面输入的汉字";left=262;top=230;right=734;bottom=263;color=5921370;dl=1;dr=1;dt=1;z=9}
)
/*}}*/

import win.debounce;
import fsys.wubiLex;
import string.conv.pinyin;

var spelling98;
var searchSpelling = win.debounce(function(str){
	if(!spelling98){
		spelling98 = fsys.wubiSpelling98();
	}
	
	winform.plusSpelling.text = ""
	winform.plusSpellingGif.background = null;
	winform.plusTitle.text = ""
	
	var code = winform.systemLex.find(str) 
	winform.plusCode.text = code ? string.upper(code) : "缺少编码";
	
	var wchars = string.split(str);
	var length  = #wchars;
	if(winform.wubiVersion=="98"){
		winform.plusSpelling.text = spelling98.find(str); 
		if(length && !#winform.plusSpelling.text){
			var codes = {};
			for(i=1;length;1){
				var code = spelling98.find( wchars[i] ) 
				if(!code) continue;
				
				code = string.match(code,"〔\s+(.+)\s+〕");
				codes[i] = string.splitEx(code,"\s+")
			}
		
			if(#codes === length && !table.some(codes,lambda(v)v===null) ){
				if(length==2){ 
					if(#codes[1]>=2 && #codes[2]>=2){
						winform.plusSpelling.text = "〔 " + codes[1][1] +" "+ codes[1][2] +" "+ codes[2][1] +" "+ codes[2][2] + " 〕" 
					}	
				}
				elseif(length==3){ 
					if(#codes[1]>=1 && #codes[2]>=1 && #codes[3]>=2){
						winform.plusSpelling.text = "〔 " + codes[1][1] +" "+ codes[2][1] +" "+ codes[3][1] +" "+ codes[3][2] + " 〕"
					}	
				}
				elseif(length==4){ 
					if(#codes[1]>=1 && #codes[2]>=1 && #codes[3]>=1 && #codes[4]>=1){
						winform.plusSpelling.text = "〔 " + codes[1][1] +" "+ codes[2][1] +" "+ codes[3][1] +" "+ codes[4][1] + " 〕"
					}	
				}
				elseif(length>4){ 
					if(#codes[1]>=1 && #codes[2]>=1 && #codes[3]>=1 && #codes[#codes]>=1){
						winform.plusSpelling.text = "〔 " + codes[1][1] +" "+ codes[2][1] +" "+ codes[3][1] +" "+ codes[#codes][1] + " 〕"
					}	
				}
			}
		}		
	}
	
	var getSpellingGifPath = function(char){
		var path = fsys.appdata("aardio\std\wubi\spelling\" 
				+ winform.wubiVersion + "\" + char + ".gif")
				
		if(!io.exist(path) ){
			path = io.exist( fsys.appdata("aardio\std\wubi\spelling\86\" + char + ".gif") )
				|| io.exist( fsys.appdata("aardio\std\wubi\spelling\98\" + char + ".gif") )
		}
		
		return path;
	}
	
	var wubiSpellingGifPathA = getSpellingGifPath( wchars[1] ); 
	if(length>1){ 
		if( ..io.exist(wubiSpellingGifPathA) ){		 
			if(length==2){ 
				var wubiSpellingGifPathB = getSpellingGifPath( wchars[2] );  
			
				if( wubiSpellingGifPathB ){ 
					var bmp = gdip.bitmap(..string.loadBuffer(wubiSpellingGifPathA)); 
					var bmp2  = gdip.bitmap(wubiSpellingGifPathB);
					var graphics = bmp.getGraphics();
					graphics.drawImage(bmp2,bmp.width/2,0);
					winform.plusSpellingGif.setBackground(bmp);
					graphics.delete();
					bmp2.dispose();
				}
			}
			elseif(length==3){ 
				var wubiSpellingGifPathB = getSpellingGifPath( wchars[2] );  
				var wubiSpellingGifPathC = getSpellingGifPath( wchars[3] ); 
					
				if( wubiSpellingGifPathB && wubiSpellingGifPathC ){ 
					var bmp = gdip.bitmap(..string.loadBuffer(wubiSpellingGifPathA)); 
					var bmp2  = gdip.bitmap(wubiSpellingGifPathB);
					var bmp3  = gdip.bitmap(wubiSpellingGifPathC);
					var graphics = bmp.getGraphics();
					graphics.drawImage(bmp2,bmp.width/4,0);
					graphics.drawImage(bmp3,bmp.width/2,0);
					winform.plusSpellingGif.setBackground(bmp);
					graphics.delete();
					bmp2.dispose();
					bmp3.dispose();
				}
			}
			elseif(length>=4){ 
				var wubiSpellingGifPathB = getSpellingGifPath( wchars[2] );  
				var wubiSpellingGifPathC = getSpellingGifPath( wchars[3] );  
				var wubiSpellingGifPathD = getSpellingGifPath( wchars[#wchars] );  
				
				if( wubiSpellingGifPathB && wubiSpellingGifPathC && wubiSpellingGifPathD ){ 
					var bmp = gdip.bitmap(..string.loadBuffer(wubiSpellingGifPathA)); 
					var bmp2  = gdip.bitmap(wubiSpellingGifPathB);
					var bmp3  = gdip.bitmap(wubiSpellingGifPathC);
					var bmp4  = gdip.bitmap(wubiSpellingGifPathD);
					var graphics = bmp.getGraphics();
					graphics.drawImage(bmp2,bmp.width/4,0);
					graphics.drawImage(bmp3,bmp.width/2,0);
					graphics.drawImage(bmp4,bmp.width/4*3,0);
					winform.plusSpellingGif.setBackground(bmp);
					graphics.delete();
					bmp2.dispose();
					bmp3.dispose();
					bmp4.dispose();
				}
			}
		}
	}
	elseif(wubiSpellingGifPathA) { 
		winform.plusSpellingGif.setBackground(string.loadBuffer(wubiSpellingGifPathA));
	}
	
	winform.plusTitle.text = "拼音：" + string.conv.pinyin(str);
},100)

import style;
import win.ui.tabs;
var menu = win.ui.tabs(winform.menuItem,winform.menuItem2)
menu.skin( style.dropdown ) 
menu.initPopup(winform.editSearch.editBox)

// 用户点选菜单项触发此事件,strip参数是点选的控件
menu.onOk = function(strip){ 
	winform.editSearch.setFocus(strip.text);
}

import web.rest.jsonLiteClient;
var http = web.rest.jsonLiteClient();
var suggestion = http.api("http://suggestion.baidu.com/su?cb=&wd={0}") 

import win.debounce;
var showSuggestion = win.debounce( 
	function(text){
		var result =  suggestion[ text ].get();
		if(#result[["s"]]){
			menu.setItemTexts(result.s,6,1) //更新下拉列表	
			var x,y,cx,cy = winform.editSearch.getPos();
			menu.popup(true,win.toScreen(winform.hwnd,x + cx/2 - 230,y+cy))	
		}
	}
)	
winform.editSearch.editBox.onChange = function(){  
	var str = string.trim(winform.editSearch.text) 
	if(string.match(str,"^[a-zA-Z]+$")){
		if(str !== menu.selText){ 
			showSuggestion(str);
		} 
		return;
	}
	
	str = string.match(str,":+");
	if(!#str) return;
		
	var char = string.charAt(str,1)
	if(!char){
		if(winform.wubiVersion){
			var verString = ({["98"]="98版";["86"]="86版";["06"]="新世纪版"})[winform.wubiVersion];
			winform.plusTitle.text = "可在上面重复输入相同单字进行练习, 当前安装的系统码表: 五笔" +verString	
		}
	
		var bmp = winform.plusSpellingGif.setBackground(null);
		if(bmp){ bmp.dispose() }
		
		winform.plusCode.text = "";
		winform.plusSpelling.text = "";	
		return;	
	}

	if(!#string.replace(str,char,"")){
		str = char;
	}

	if(#str)searchSpelling(str)
}

winform.editSearch.editBox.setFocus()

if( "IInkEdit" == winform.inkEdit.getControlTypeName() ){
	var inkEdit = winform.inkEdit.getControl(); 
	inkEdit.Enabled=true; //启用
	inkEdit.UseMouseForInput=true;//允许鼠标输入
	inkEdit.BackColor=0xDCF8FF; //背景色
	inkEdit.RecognitionTimeout=1000 //识别速度,写的快这个值可以改小
	inkEdit.InkMode=inkEdit.IEM_Ink;//收集墨迹和手势
	inkEdit.InkInsertMode=0  //文本模式
	inkEdit.Locked=false;//关闭只读 
	inkEdit.SelFontName="Tahoma"; //字体
	inkEdit.SelFontSize=12; //文字大小
	inkEdit.SelInksDisplayMode = 1
	
	inkEdit.Change = function(){
		winform.editSearch.text = inkEdit.Text;
		inkEdit.text = "";
	}
}
else {
	var wb = winform.inkEdit.getControl();
	wb.Navigate("about:当前系统不支持Microsoft Ink")
}

winform.show() 
	
subscribe("wubi.system.lex.changed",function(...){
	winform.systemLex = fsys.wubiLex()
	var ver  = winform.systemLex.getVersion()
	var verString = ({["98"]="98版";["86"]="86版";["06"]="新世纪版"})[ver];
	winform.plusTitle.text = "根据当前安装的系统码表( " +verString + " )反查上面输入的汉字，可直接输入拼音"
	
	var wubiSpellingGifPath86 = fsys.appdata("aardio\std\wubi\spelling\86");
	var wubiSpellingGifPath98 = fsys.appdata("aardio\std\wubi\spelling\98"); 
	if( !(..io.exist(wubiSpellingGifPath86) && ..io.exist(wubiSpellingGifPath98)) ){ 
		winform.plusDownloadSpellingGif.hide = ver == "06"
	}
	else {
		winform.plusDownloadSpellingGif.hide = true;
	}
	
	winform.wubiVersion = ver;
	winform.editSearch.text = "";
	winform.plusCode.text =""
	winform.plusSpellingGif.background = null;
	winform.plusSpelling.text = "";
} )

if(mainForm){
	mainForm.tabs.strips[2].disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
	win.delay(200);	
}

winform.plusTitle.text = "请稍候几秒,正在加载字根数据库..."

..win.setTimeout(
	function(){
		import fsys.wubiSpelling98;
		winform.plusSpelling.setFont(h=-26;name='98WB-2');
	
		publish("wubi.system.lex.changed");	
		if(mainForm) mainForm.tabs.strips[2].disabledText = null;
	},100
)

winform.plusDownloadSpellingGif.skin(style.primaryButton)

import sevenZip.lzma.httpFile;
winform.plusDownloadSpellingGif.oncommand = function(id,event){
	var downbox = inet.downBox(winform.hwnd,"下载五笔拆分图解字库",-1);
	var saveDir = fsys.appdata("/aardio/std/wubi/download")
	var extraDir = fsys.appdata("/aardio/std/wubi/")
	
	var bmp = winform.plusSpellingGif.setBackground(null);
	if(bmp){ bmp.dispose() }
	
	if( sevenZip.lzma.httpFile.download("http://download.aardio.com/wubi/spelling.tar.lzma"
		,"下载五笔拆分图解字库",saveDir,extraDir,"spelling.tar.lzma",winform) ){
		winform.editSearch.editBox.onChange()
		winform.plusDownloadSpellingGif.hide = true;
	}
}

win.loopMessage();