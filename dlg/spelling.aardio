
import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="五笔码表管理﻿工具";right=1040;bottom=610;bgcolor=16777215)
winform.add(
editSearch={cls="plus";left=287;top=191;right=712;bottom=226;border={bottom=1;color=-16744448};dl=1;dr=1;dt=1;editable=1;font=LOGFONT(h=-16);tabstop=1;textPadding={top=6};z=2};
inkEdit={cls="atlax";text="InkEd.InkEdit";left=743;top=224;right=999;bottom=491;dr=1;dt=1;z=4};
lbInk={cls="static";text="手写输入:";left=743;top=201;right=863;bottom=218;dr=1;dt=1;transparent=1;z=5};
plusCode={cls="plus";left=265;top=277;right=499;bottom=327;align="right";dl=1;dr=1;dt=1;font=LOGFONT(h=-37);textPadding={right=10};z=3};
plusDownloadSpellingGif={cls="plus";text='\uF019  下载五笔拆分详细图解字库';left=438;top=508;right=717;bottom=544;db=1;dr=1;font=LOGFONT(h=-16;name='FontAwesome');hide=1;notify=1;tabstop=1;z=6};
plusSpelling={cls="plus";left=311;top=348;right=688;bottom=404;color=255;dl=1;dr=1;dt=1;z=1};
plusSpellingGif={cls="plus";left=499;top=277;right=719;bottom=333;dl=1;dr=1;dt=1;repeat="scale";z=8};
plusTitle={cls="plus";text="根据当前安装的系统码表反查上面输入的汉字";left=276;top=230;right=723;bottom=263;color=5921370;dl=1;dr=1;dt=1;z=7}
)
/*}}*/

import win.debounce;
import fsys.wubiLex;
import string.conv.pinyin;

var spelling98;
var searchSpelling = win.debounce(function(word){
	if(!spelling98){
		spelling98 = fsys.wubiSpelling98();
	}
	
	winform.plusSpelling.text = ""
	winform.plusSpellingGif.background = null;
	winform.plusTitle.text = ""
	
	var code = winform.systemLex.find(word) 
	winform.plusCode.text = code ? string.upper(code) : "缺少编码";
	
	var dWordSpelling = string.len(winform.plusCode.text)==4 && string.len(word) == 2 
	if(winform.wubiVersion=="98"){
		winform.plusSpelling.text = spelling98.find(word); 
		if(!#winform.plusSpelling.text && dWordSpelling ){
			var a,b = string.match(word,"(:)(:)")
			if(a&&b){
				var as = spelling98.find(a)
				var bs = spelling98.find(b)
				if(as&&bs){
					var as1,as2 = string.match(as,"〔\s*(\S+)\s+(\S+)")
					var bs1,bs2 = string.match(bs,"〔\s*(\S+)\s+(\S+)")
						
					if(as1&&as2&&bs1&&bs2){
						winform.plusSpelling.text = "〔 " + as1 +" "+ as2 +" "+ bs1 +" "+ bs2 + " 〕"
					}	
				}
			}
		}		
	}

	if(dWordSpelling){
		var a,b = string.match(word,"(:)(:)") 
		if(a&&b){ 
			var wubiSpellingGifPathA = fsys.appdata("aardio\std\wubi\spelling\spelling\" 
				+ winform.wubiVersion + "\" + a + ".gif");  
			var wubiSpellingGifPathB = fsys.appdata("aardio\std\wubi\spelling\spelling\" 
				+ winform.wubiVersion + "\" + b + ".gif"); 
				
			if( ..io.exist(wubiSpellingGifPathA) 
				&& ..io.exist(wubiSpellingGifPathB)  ){ 
				var bmp = gdip.bitmap(..string.loadBuffer(wubiSpellingGifPathA)); 
				var bmp2  = gdip.bitmap(wubiSpellingGifPathB);
				var graphics = bmp.getGraphics();
				graphics.drawImage(bmp2,bmp.width/2,0);
				winform.plusSpellingGif.setBackground(bmp);
				graphics.delete();
				bmp2.dispose();
			}
		}
	}
	else {
		var wubiSpellingGifPath = fsys.appdata("aardio\std\wubi\spelling\spelling\" 
			+ winform.wubiVersion + "\" + ..string.left(word,1,true) + ".gif");  
		if( ..io.exist(wubiSpellingGifPath)  ){  
			winform.plusSpellingGif.setBackground(string.loadBuffer(wubiSpellingGifPath));
		}	
	}
	
	winform.plusTitle.text = "拼音：" + string.conv.pinyin(word);
},100)

winform.editSearch.editBox.onChange= function(){  
	var word = string.trim(winform.editSearch.text) 
	var char = string.match(word,":")
	
	if(!char){
		if(winform.wubiVersion){
			var verString = ({["98"]="98版";["86"]="86版";["06"]="新世纪版"})[winform.wubiVersion];
			winform.plusTitle.text = "可在上面重复输入相同单字进行练习, 当前安装的系统码表: 五笔" +verString	
		}
	
		var bmp = winform.plusSpellingGif.setBackground(null);
		if(bmp){ bmp.dispose() }
		
		winform.plusCode.text = "";
		winform.plusSpelling.text = "";	
		return;	
	}

	if(!#string.replace(word,char,"")){
		word = char;
	}

	searchSpelling(word)
}

winform.editSearch.editBox.setFocus()

if( "IInkEdit" == winform.inkEdit.getControlTypeName() ){
	var inkEdit = winform.inkEdit.getControl(); 
	inkEdit.Enabled=true; //启用
	inkEdit.UseMouseForInput=true;//允许鼠标输入
	inkEdit.BackColor=0xDCF8FF; //背景色
	inkEdit.RecognitionTimeout=1000 //识别速度,写的快这个值可以改小
	inkEdit.InkMode=inkEdit.IEM_InkAndGesture;//收集墨迹和手势
	inkEdit.InkInsertMode=0  //文本模式
	inkEdit.Locked=false;//关闭只读 
	inkEdit.SelFontName="Tahoma"; //字体
	inkEdit.SelFontSize=12; //文字大小
	inkEdit.SelInksDisplayMode = 1
	
	inkEdit.Change = function(){
		winform.editSearch.text = inkEdit.Text;
		inkEdit.text = "";
	}
}
else {
	var wb = winform.inkEdit.getControl();
	wb.Navigate("about:当前系统不支持Microsoft Ink")
}

winform.show() 
	
subscribe("wubi.system.lex.changed",function(...){
	winform.systemLex = fsys.wubiLex()
	var ver  = winform.systemLex.getVersion()
	var verString = ({["98"]="98版";["86"]="86版";["06"]="新世纪版"})[ver];
	winform.plusTitle.text = "根据当前安装的系统码表( " +verString + " )反查上面输入的汉字"
	
	var wubiSpellingGifPath86 = fsys.appdata("aardio\std\wubi\spelling\spelling\86");
	var wubiSpellingGifPath98 = fsys.appdata("aardio\std\wubi\spelling\spelling\98"); 
	if( !(..io.exist(wubiSpellingGifPath86) && ..io.exist(wubiSpellingGifPath98)) ){ 
		winform.plusDownloadSpellingGif.hide = ver == "06"
	}
	else {
		winform.plusDownloadSpellingGif.hide = true;
	}
	
	winform.wubiVersion = ver;
	winform.editSearch.text = "";
	winform.plusCode.text =""
	winform.plusSpellingGif.background = null;
	winform.plusSpelling.text = "";
} )

if(mainForm){
	mainForm.tabs.strips[2].disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
	win.delay(200);	
}

winform.plusTitle.text = "请稍候几秒,正在加载字根数据库..."

..win.setTimeout(
	function(){
		import fsys.wubiSpelling98;
		winform.plusSpelling.setFont(h=-26;name='98WB-2');
	
		publish("wubi.system.lex.changed");	
		if(mainForm) mainForm.tabs.strips[2].disabledText = null;
	},100
)

import style;
winform.plusDownloadSpellingGif.skin(style.button)

import sevenZip.lzma.httpFile;
winform.plusDownloadSpellingGif.oncommand = function(id,event){
	var downbox = inet.downBox(winform.hwnd,"下载五笔拆分图解字库",-1);
	var saveDir = fsys.appdata("/aardio/std/wubi/download")
	var extraDir = fsys.appdata("/aardio/std/wubi/spelling")
	
	var bmp = winform.plusSpellingGif.setBackground(null);
	if(bmp){ bmp.dispose() }
	
	if( sevenZip.lzma.httpFile.download("http://download.aardio.com/wubi/spelling.tar.lzma"
		,"下载五笔拆分图解字库",saveDir,extraDir,"spelling.tar.lzma",winform) ){
		winform.editSearch.editBox.onChange()
		winform.plusDownloadSpellingGif.hide = true;
	}
}

win.loopMessage();