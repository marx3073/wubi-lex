//RUNAS//
import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
mainForm = win.form(cls="AARDIO_WUBI_FORM";text="微软五笔助手( Win10  )";right=1040;bottom=642;bgcolor=15793151;border="none")
mainForm.add(
caption={cls="bkplus";text="Win10 五笔助手";left=48;top=11;right=171;bottom=29;color=15780518;dl=1;dt=1;font=LOGFONT(h=-14);z=5};
custom={cls="custom";left=-1;top=40;right=1040;bottom=648;bgcolor=16777215;db=1;dl=1;dr=1;dt=1;z=1};
logo={cls="bkplus";text='\uF0AD';left=19;top=9;right=48;bottom=34;color=15780518;dl=1;dt=1;font=LOGFONT(h=-18;name='FontAwesome');z=4};
nav1={cls="plus";text="词库管理";left=173;top=5;right=294;bottom=40;align="left";color=16777215;dl=1;dt=1;font=LOGFONT(h=-16;name='FontAwesome');iconStyle={align="left";font=LOGFONT(h=-19;name='FontAwesome');padding={left=15;top=3}};iconText='\uF00A';notify=1;paddingLeft=1;paddingRight=1;paddingTop=3;textPadding={left=42;top=3};x=0.5;y=0.2;z=3};
nav2={cls="plus";text="字根反查";left=294;top=5;right=415;bottom=40;align="left";color=16777215;dl=1;dt=1;font=LOGFONT(h=-16;name='FontAwesome');iconStyle={align="left";font=LOGFONT(h=-19;name='FontAwesome');padding={left=15;top=3}};iconText='\uF002';notify=1;paddingLeft=1;paddingRight=1;paddingTop=3;textPadding={left=42;top=3};x=0.5;y=0.2;z=6};
titleBar={cls="bkplus";left=0;top=0;right=1044;bottom=41;bgcolor=14977280;dl=1;dr=1;dt=1;z=2}
)
/*}}*/

import win.ui.simpleWindow;
win.ui.simpleWindow( mainForm );

import win.ui.tabs;
var tbs = win.ui.tabs( 
	mainForm.nav1,
	mainForm.nav2,
);
	
tbs.skin({
	foreground={
		active=0xFFFFFFFF;
		default=0x00FFFFFF;
		hover=0x38FFFFFF
	};
	color={
		default=0xFFFFFFFF; 
	};
	checked={
		foreground={default=0xFFFFFFFF;};
		color={default=0xFF42A875;};
	}
})

tbs.add({
	text="短语词库";
	iconText='\uF0AE'; 
})

tbs.add({
	text="设置";
	iconText='\uF0AD';
})
	
tbs.add({
	text="帮助";
	iconText='\uF06A';
	hasCloseButton = true;
})
	
tbs.loadForm(1,"\dlg\lex.aardio" ); 
tbs.loadForm(2,"\dlg\spelling.aardio" ); 
tbs.loadForm(3,"\dlg\phrase.aardio" ); 
tbs.loadForm(4,"\dlg\setting.aardio" ); 
tbs.loadForm(5,"\dlg\help.aardio" ); 
tbs.selIndex = 1;

import win.util.tray;
mainForm.onMinimize = function(lParam){
	mainForm.tray = win.util.tray(mainForm)  
	mainForm.modifyStyleEx(0x40000/*_WS_EX_APPWINDOW*/,0x80/*_WS_EX_TOOLWINDOW*/)
	mainForm.show(false); 
	return true; 
}

mainForm.onDestroy = function(){
	if(mainForm.tray){
		mainForm.tray.delete()
	}
}

import win.ui.menu;
mainForm.wndproc = {
	[0xACCF/*_WM_TRAYMESSAGE*/ ] = function(hwnd,message,wParam,lParam){
		if( lParam = 0x205/*_WM_RBUTTONUP*/ ){
			win.setForeground(mainForm.hwnd)
			mainForm.popmenu = win.ui.popmenu(mainForm);//创建弹出菜单
			mainForm.popmenu.add('切换词库',function(id){
				tbs.selIndex = 1;
				mainForm.show();
			});
			mainForm.popmenu.add('管理短语',function(id){
				tbs.selIndex = 3;
				mainForm.show();
			});
			mainForm.popmenu.add('字根反查',function(id){
				tbs.selIndex = 2;
				mainForm.show();
				tbs.selForm.editSearch.editBox.setFocus();
			});
			mainForm.popmenu.add('打开本软件源代码',function(id){
				import process;
				process.openUrl("https://github.com/aardio/wubi-lex-tool")
			});
			mainForm.popmenu.add('下载最新版本',function(id){
				import process;
				process.openUrl("https://github.com/aardio/wubi-lex-tool/releases")
			});
			mainForm.popmenu.add('退出',function(id){
				mainForm.close()
			});
			
			var pt = ::POINT();
			::User32.GetCursorPos(pt);
			mainForm.popmenu.popup(pt.x,pt.y,true)
		}
		elseif( lParam = 0x202/*_WM_LBUTTONUP*/ ) {
			mainForm.show();
			win.setForeground(mainForm.hwnd);
			
			if(tbs.selIndex = 2){
				tbs.selForm.editSearch.editBox.setFocus();
			}
		} 	
	}
}
	
subscribe("spellingHotkeyChange",function(...){
	if(#config.hotkey.spelling){
		if(mainForm.hkSpellingId){
			mainForm.unreghotkey(mainForm.hkSpellingId)
			mainForm.hkSpellingId = null;
		}
		
		mainForm.hkSpellingId = mainForm.reghotkey( function(id,mod,vk){ 
			var hwndForeground = win.getForeground()
			if(hwndForeground == mainForm.hwnd){
				mainForm.onMinimize();
			}
			else {
				tbs.selIndex = 2;
				mainForm.show();
				win.setForeground(mainForm.hwnd);
				tbs.selForm.editSearch.editBox.setFocus();		
			} 

		},config.hotkey.spelling[1],config.hotkey.spelling[2])    	
	}
} )
publish("spellingHotkeyChange");

mainForm.tabs = tbs;
mainForm.show();
return win.loopMessage(); 
